// SPDX-License-Identifier: UNLICENSCED
pragma solidity ^0.8.0;
// this is a proxy contract mainly copy from https://solidity-by-example.org/app/upgradeable-proxy/
contract Vacancy_Proxy {
    // should be company's address that issues this info
    address public owner;
    bool public isInitialized=false;

    string private job_title;
    // stores descriptions 1./2./3. ...
    string[] DESCRIPTION; 
    uint256 private description_index;
    uint256 private numOfDescription;
    uint256 public numOfApplications; 

    // stores cv contracts' addresses
    address[] private APPLICATIONS;
    // stores appliers' addresses
    address[] private APPLIERS;
    //stores applier's idea
    mapping (address => uint256)  APPLIER_ID;


    // -1 for unknown preimage
    // 0x360894a13ba1a3210667c828492db98dca3e2076cc3735a920a3ca505d382bbc
    bytes32 public constant IMPLEMENTATION_SLOT = bytes32(uint(keccak256("eip1967.proxy.implementation")) - 1);
    // 0xb53127684a568b3173ae13b9f8a6016e243e63b6e8ee1178d6a717850b5d6103
    bytes32 private constant OWNER_SLOT = bytes32(uint(keccak256("eip1967.proxy.admin")) - 1);


    constructor(address caller, address current){
        owner=caller;
        StorageSlot.getAddressSlot(OWNER_SLOT).value=caller;
        StorageSlot.getAddressSlot(IMPLEMENTATION_SLOT).value=current;
    }

    function _delegate(address _implementation) internal virtual {
        assembly {
            // Copy msg.data. We take full control of memory in this inline assembly
            // block because it will not return to Solidity code. We overwrite the
            // Solidity scratch pad at memory position 0.

            // calldatacopy(t, f, s) - copy s bytes from calldata at position f to mem at position t
            // calldatasize() - size of call data in bytes
            calldatacopy(0, 0, calldatasize())

            // Call the implementation.
            // out and outsize are 0 because we don't know the size yet.

            // delegatecall(g, a, in, insize, out, outsize) -
            // - call contract at address a
            // - with input mem[in…(in+insize))
            // - providing g gas
            // - and output area mem[out…(out+outsize))
            // - returning 0 on error (eg. out of gas) and 1 on success
            let result := delegatecall(gas(), _implementation, 0, calldatasize(), 0, 0)

            // Copy the returned data.
            // returndatacopy(t, f, s) - copy s bytes from returndata at position f to mem at position t
            // returndatasize() - size of the last returndata
            returndatacopy(0, 0, returndatasize())

            switch result
            // delegatecall returns 0 on error.
            case 0 {
                // revert(p, s) - end execution, revert state changes, return data mem[p…(p+s))
                revert(0, returndatasize())
            }
            default {
                // return(p, s) - end execution, return data mem[p…(p+s))
                return(0, returndatasize())
            }
        }
    }
    
    function setImplementation(address _implementation) public{
        require(msg.sender==owner,"NOT owner!");
        require(_implementation.code.length > 0, "implementation is not contract");
        StorageSlot.getAddressSlot(IMPLEMENTATION_SLOT).value = _implementation;
    }

    function _getImplementation() public view returns (address) {
        return StorageSlot.getAddressSlot(IMPLEMENTATION_SLOT).value;
    }

    function _getOwner() public view returns (address) {
        return StorageSlot.getAddressSlot(OWNER_SLOT).value;
    }

    function upgradeTo(address _implementation) public{
        require(msg.sender==owner,"NOT owner!");
        setImplementation(_implementation);
    }

    function _fallback() private {
        _delegate(_getImplementation());
    }

    fallback() external payable {
        _fallback();
    }

    receive() external payable {
        _fallback();
    }

}

library StorageSlot {
    struct AddressSlot {
        address value;
    }

    function getAddressSlot(
        bytes32 slot
    ) internal pure returns (AddressSlot storage r) {
        assembly {
            r.slot := slot
        }
    }
}
